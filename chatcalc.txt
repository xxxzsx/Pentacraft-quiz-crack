$${

  split(" ",%CHATCLEAN%,&splittedchat[])
  &expression = join("",&splittedchat)



  ifmatches(%&expression%,"\d[\+\-\*xх\/\%]-?\d")
    match(%&expression%,"(-?\d+)([\+\-\*xх\/\%])(-?\d+)",{#x,&operation,#y})

    unset(&answer)
    if(%&operation% == "-")
      #answer = #x - #y
    elseif(%&operation% == "+")
      #answer = #x + #y
    elseif((%&operation% == "*") || (%&operation% == "x") || (%&operation% == "х"))
      #answer = #x * #y
    elseif((%&operation% == "/") || (%&operation% == "%"))
      #answer = #x / #y
      if(%#x% != #y * #answer)
        #remainder = #x - (#y * #answer)
        &answer = " + %#remainder%/%#y%"
      endif
    endif

    log("&e%#x% %&operation% %#y% = %#answer%%&answer%")
  endif



  ifmatches(%CHATCLEAN%,"^\[Викторина] Первый игрок, решивший .* . .*, выиграет .*\.00 монет")
    match(%CHATCLEAN%,"(-?\d*) (.) (\d*), выиграет (.*)\.00 ",{#x,&operation,#y,#reward})

    if(%#reward% < 453)
      log("&eВыигрыш слишком мал")
      stop()
    endif

    if(%&operation% == "-")
      #y = -#y
    endif
    #answer = #x + #y

    random(#answerdelay,9000,12000)
    if(%#reward% > 912)
      #answerdelay = #answerdelay - 3000
    endif
    log("&eОтвет через %#answerdelay%мс")

    random(#makemistake,1,4)
    if((%#makemistake% == 1) && ((%#x% < -100) || (%#x% > 100)) && (%#y% > 100) && ((%#answer% < -100) || (%#answer% > 100)) && (%#reward% <= 692))
      random(#mistake,1,8)
      if(%#mistake% == 1)
        #wronganswer = #answer + 20
      elseif(%#mistake% == 3)
        #wronganswer = #answer - 120
      elseif(%#mistake% == 5)
        #wronganswer = #answer + 100
      elseif(%#mistake% == 7)
        #wronganswer = -#answer
      else
        #wronganswer = #answer + #mistake
      endif

      random(#betweenanswersdelay,2500,4500)
      #wronganswerdelay = #answerdelay - #betweenanswersdelay
      #answerdelay = #answerdelay - #wronganswerdelay
      log("&eИмитация ошибки через %#wronganswerdelay%мс")

      wait("%#wronganswerdelay%ms")
      echo(%#wronganswer%)
    endif

    if(%#answer% == 0)
      stop()
    endif

    wait("%#answerdelay%ms")
    echo("%#answer%")
    wait("273ms")
    echo("/money")
    stop()
  endif



  ifmatches(%CHATCLEAN%,"(^\[Викторина] Вы выиграли)|(Вы получили \d)")
    match(%CHATCLEAN%,"ли (.*)\.00 ",{#reward})
    achievementget("+%#reward% монет",speckled_melon)
    stop()
  endif

}$$